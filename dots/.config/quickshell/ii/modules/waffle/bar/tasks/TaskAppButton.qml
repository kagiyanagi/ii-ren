pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Layouts
import qs.services
import qs.modules.common
import qs.modules.common.functions
import qs.modules.waffle.looks
import qs.modules.waffle.bar
import Quickshell

AppButton {
    id: root

    required property var appEntry
    readonly property bool isSeparator: (root.appEntry?.appId ?? "") === "SEPARATOR"
    property var desktopEntry: DesktopEntries.heuristicLookup(root.appEntry?.appId ?? "")

    Timer {
        // Retry looking up the desktop entry if it failed (e.g. database not loaded yet)
        property int retryCount: 5
        interval: 1000
        running: !root.isSeparator && root.desktopEntry === null && retryCount > 0
        repeat: true
        onTriggered: {
            retryCount--;
            root.desktopEntry = DesktopEntries.heuristicLookup(root.appEntry?.appId ?? "");
        }
    }

    property bool active: root.appEntry?.toplevels?.some(t => t?.activated) ?? false
    property bool hasWindows: (root.appEntry?.toplevels?.length ?? 0) > 0

    signal hoverPreviewRequested()
    signal hoverPreviewDismissed()

    multiple: (root.appEntry?.toplevels?.length ?? 0) > 1
    checked: active
    iconName: AppSearch.guessIcon(root.appEntry?.appId ?? "")
    tryCustomIcon: false

    onHoverTimedOut: {
        root.hoverPreviewRequested();
    }

    onClicked: {
        root.hoverTimer.stop(); // Prevents preview showing up when clicking to focus
        if (root.multiple) {
            root.hoverPreviewRequested();
        } else if (root.appEntry?.toplevels?.length === 1) {
            root.appEntry.toplevels[0]?.activate();
        } else {
            root.desktopEntry?.execute();
        }
    }

    middleClickAction: () => {
        root.desktopEntry?.execute();
    }

    altAction: () => {
        root.hoverPreviewDismissed();
        root.hoverTimer.stop();
        contextMenu.active = true;
    }

    // Active indicator
    Rectangle {
        id: activeIndicator
        opacity: root.hasWindows ? 1 : 0
        anchors {
            horizontalCenter: root.background.horizontalCenter
            bottom: root.background.bottom
            bottomMargin: 2
        }

        implicitWidth: root.active ? 16 : 6
        implicitHeight: 3
        radius: height / 2

        color: root.active ? Looks.colors.accent : Looks.colors.accentUnfocused

        Behavior on implicitWidth {
            animation: Looks.transition.enter.createObject(this)
        }
        Behavior on color {
            animation: Looks.transition.color.createObject(this)
        }
        Behavior on opacity {
            animation: Looks.transition.opacity.createObject(this)
        }
    }

    BarToolTip {
        extraVisibleCondition: root.shouldShowTooltip && !root.hasWindows
        text: root.desktopEntry ? root.desktopEntry.name : (root.appEntry?.appId ?? "")
    }

    BarMenu {
        id: contextMenu

        model: [
            ...(((root.desktopEntry?.actions?.length ?? 0) > 0) ? root.desktopEntry.actions.map(action => ({
                iconName: action.icon,
                text: action.name,
                action: () => {
                    action?.execute?.();
                }
            })).concat({ type: "separator" }) : []),
            {
                iconName: root.iconName,
                text: root.desktopEntry ? root.desktopEntry.name : StringUtils.toTitleCase(root.appEntry?.appId ?? ""),
                monochromeIcon: false,
                action: () => {
                    root.desktopEntry?.execute();
                }
            },
            {
                iconName: root.appEntry?.pinned ? "pin-off" : "pin",
                text: root.appEntry?.pinned ? Translation.tr("Unpin from taskbar") : Translation.tr("Pin to taskbar"),
                action: () => {
                    if (root.appEntry?.appId)
                        TaskbarApps.togglePin(root.appEntry.appId);
                }
            },
            ...((root.appEntry?.toplevels?.length ?? 0) > 0 ? [{
                iconName: "dismiss",
                text: root.multiple ? Translation.tr("Close all windows") : Translation.tr("Close window"),
                action: () => {
                    for (let toplevel of (root.appEntry?.toplevels ?? [])) {
                        toplevel?.close?.();
                    }
                }
            }] : []),
        ]
    }
}
